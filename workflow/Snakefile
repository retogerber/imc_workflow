import re
import csv
import os
import glob
import pandas as pd 

include: "rules/common.smk"

# working directory
workdir: "."

# config file name
configfile: "config/config.yaml"

# report file
report: "report/report.rst"

# mcd files, all files in directory 'results/raw' are used
mcd_files=glob.glob("results/raw/*.mcd")
mcd_files=[ os.path.basename(s).replace(".mcd","") for s in mcd_files  ]


### used parameters
## please adjust the configfile as needed

## Filtering:
# threshold for filtering
filter_thres=config["filtering"]["filter_thres"]
# type of filtering
filter_subtraction_type=config["filtering"]["filter_subtraction_type"]
## Segmentation
# list of nuclear channels used in segmentation
nuc_channels=config["segmentation"]["nuc_channels"]
# list of membrane channels used in segmentation
mem_channels=config["segmentation"]["mem_channels"]
# size of each pixel in micrometer
image_pixelsize=config["segmentation"]["image_pixelsize"]
# postprocessing parameters used in mesmer
mesmer_postprocessing_paramsfile=config["segmentation"]["mesmer_postprocessing_paramsfile"]

## steinbock, which version is used
steinbock_container=config["steinbock_container"]
## additional metadata of channels
channel_metadata_file=config["channel_metadata_file"]


## Cell level filtering
min_cell_area=config["min_cell_area"]

## sample level metadata
sample_metadata=config["sample_metadata"]
cols_for_batch=config["cols_for_batch"]

with open(sample_metadata) as f:
    has_metadata = len(pd.read_csv(f).index) > 1
## cell mixing score number of neighbors
k_cms=config["k_cms"]

## annotation
clustering_annotation_file=config["clustering_annotation_file"]
clustering_used=config["clustering_used"]
if clustering_used == "" and not has_metadata:
    get_annot_spe="raw/SPE_combined_markedfilt.rds"
    get_vis_html=["filter"]
elif clustering_used == "" and has_metadata:
    get_annot_spe="filt/SPE_combined_02.rds"
    get_vis_html=["filter","batch","batch_evaluation","cluster"]
else:
    get_annot_spe="filt/SPE_combined_03.rds"
    get_vis_html=["filter","batch","batch_evaluation","cluster"]

print(get_vis_html)

### Start of workflow.

#print(".mcd files:")
#print(mcd_files)

rule all:
    input:
        expand("results/spe/{v}",v=get_annot_spe),
        expand("results/html/visualize_{v}_spe.html",v=get_vis_html),
        "config/samples.csv",
        "results/html/.empty"



rule extract_images_from_mcd:
    params:
        #imgout="results/img"
        imgout = lambda w, output: os.path.join(*os.path.normpath(output[3]).split(os.path.sep)[:2])
    input:
        mcd_files="results/raw/{mcd}.mcd"
    output:
        imginfo_out="results/images_csv/{mcd}_images.csv",
        tmpdir=directory("results/raw_tmp/{mcd}"),
        sumpan="results/summary_panels/{mcd}_summary.csv",
        img_dir=directory("results/img/{mcd}")
    log:
        stdout="logs/extract_images_from_mcd/{mcd}.stdout",
        stderr="logs/extract_images_from_mcd/{mcd}.stderr"
    container:
        steinbock_container
    shell:
        "mkdir -p {output.tmpdir} > {log.stdout} 2> {log.stderr} "
        " && cp {input.mcd_files} {output.tmpdir} >> {log.stdout} 2>> {log.stderr}"
        " && steinbock preprocess imc panel -o {output.sumpan} --mcd {output.tmpdir} --txt {output.tmpdir} >> {log.stdout} 2>> {log.stderr} "
        " && steinbock preprocess imc images --hpf 50 --mcd {output.tmpdir} --txt {output.tmpdir} --panel {output.sumpan} --imgout {output.img_dir} --infoout {output.imginfo_out} >> {log.stdout} 2>> {log.stderr} "
        " && mv {output.img_dir}/*.tiff {params.imgout} >> {log.stdout} 2>> {log.stderr}"

checkpoint create_image_csv:
    input:
        sumpan=expand("results/images_csv/{mcd}_images.csv", mcd=mcd_files)
    output:
        imcsv="results/images.csv"
    log:
        stdout="logs/create_image_csv/log.stdout",
        stderr="logs/create_image_csv/log.stderr"
    container:
        steinbock_container
    shell:
        "first_file=$( echo {input.sumpan}  ) && first_file=( ${{first_file[0]}} ) && head -n 1 ${{first_file[0]}} > {output.imcsv} 2> {log.stderr} && tail -n+2 -q {input.sumpan} >> {output.imcsv} 2>> {log.stderr}"

rule create_samples_csv:
    input:
        imcsv="results/images.csv"
    output:
        samcsv="config/samples.csv"
    log:
        stdout="logs/create_samples_csv/log.stdout",
        stderr="logs/create_samples_csv/log.stderr"
    container:
        steinbock_container
    shell:
        "cat {input.imcsv} | cut -d, -f1 | sed 1d | sed 's/.tiff//g' > {output.samcsv}  2> {log.stderr}"

rule create_individual_summary_panels:
    params:
        nuc_channels=nuc_channels,
        mem_channels=mem_channels
    input:
        summary_panel_in=mcd_summary_panel_from_sample_name
    output:
        summary_panels="results/summary_panels/{sample}_summary.csv"
    log:
        stdout="logs/create_individual_summary_panels/{sample}.stdout",
        stderr="logs/create_individual_summary_panels/{sample}.stderr"
    container:
        steinbock_container
    shell:
        "workflow/scripts/create_individual_summary_panel.sh -s '{input.summary_panel_in}' -p '{output.summary_panels}' -n '{params.nuc_channels}' -m '{params.mem_channels}' > {log.stdout} 2> {log.stderr}"


rule background_subtraction:
    params:
        thres=filter_thres,
        subtraction_type=filter_subtraction_type
    input:
        img = get_img_from_images_csv,
        mcd_files=mcd_name_from_sample_name
    output:
        "results/img_filt/{sample}.tiff"
    log:
        stdout="logs/background_subtraction/{sample}.stdout",
        stderr="logs/background_subtraction/{sample}.stderr"
    conda:
        "workflow/envs/filter_hot_pixels.yaml"
    script:
        "workflow/scripts/background_subtraction.py > {log.stdout} 2> {log.stderr}"


rule steinbock_separate_images_for_mask:
    input:
        img_dir=expand("results/img/{mcd}",mcd=mcd_files),
        img_files="results/img/{sample}.tiff",
        files=get_img_samplenames
    output:
        output_dir = directory("results/temp_img/{sample}"),
        output_files = "results/temp_img/{sample}/{sample}.tiff",
    log:
        stdout="logs/steinbock_separate_image_for_mask/{sample}.stdout",
        stderr="logs/steinbock_separate_image_for_mask/{sample}.stderr"
    container:
        steinbock_container
    shell:
        "mkdir -p {output.output_dir} > {log.stdout} 2> {log.stderr} "
        " && ln -rs {input.files} {output.output_files} >> {log.stdout} 2>> {log.stderr} "


rule combine_panel_csv:
    input:
        "results/summary_panels/{sample}_summary.csv"
    output:
        "results/temp_summary_panels/{sample}_summary.csv"
    log:
        stdout="logs/combine_panel_csv/{sample}.stdout",
        stderr="logs/combine_panel_csv/{sample}.stderr"
    container:
        steinbock_container
    shell:
        "cp {input} {output} > {log.stdout} 2> {log.stderr}"

rule steinbock_create_mask:
    params:
        pixelsize = image_pixelsize,
        postprocessing_file=mesmer_postprocessing_paramsfile
    input:
        img = "results/temp_img/{all_samples}",
        img_files = "results/temp_img/{all_samples}/{all_samples}.tiff",
        panel = "results/temp_summary_panels/{all_samples}_summary.csv"
    output:
        tmp_dir=temp(directory("results/temp_masks_init/{all_samples}")),
        tmp_masks=temp("results/temp_masks_init/{all_samples}/{all_samples}.tiff"),
        masks="results/masks/{all_samples}.tiff"
    log:
        stdout="logs/steinbock_create_mask/{all_samples}.stdout",
        stderr="logs/steinbock_create_mask/{all_samples}.stderr"
    container:
        steinbock_container
    threads: 8
    shell: 
        "mkdir -p {output.tmp_dir} > {log.stdout} 2> {log.stderr} "
        " && OPENBLAS_NUM_THREADS={threads} steinbock segment deepcell --minmax --zscore --pixelsize {params.pixelsize} --postprocess {params.postprocessing_file} --img {input.img} --panel {input.panel} -o {output.tmp_dir} >> {log.stdout} 2>> {log.stderr} "
        " && cp {output.tmp_masks} {output.masks} >> {log.stdout} 2>> {log.stderr} "

rule steinbock_separate_masks_for_intensities:
    input:
        mask = "results/masks/{all_samples}.tiff",
    output:
        tmp_dir=directory("results/temp_masks/{all_samples}"),
        tmp_masks="results/temp_masks/{all_samples}/{all_samples}.tiff"
    log:
        stdout="logs/steinbock_separate_masks_for_intensities/{all_samples}.stdout",
        stderr="logs/steinbock_separate_masks_for_intensities/{all_samples}.stderr"
    container:
        steinbock_container
    shell:
        "mkdir -p {output.tmp_dir} > {log.stdout} 2> {log.stderr} "
        " && ln -rs {input.mask} {output.tmp_masks} >> {log.stdout} 2>> {log.stderr} "

rule steinbock_create_intensities:
    input:
        img = "results/temp_img/{all_samples}",
        img_files = "results/temp_img/{all_samples}/{all_samples}.tiff",
        panel = "results/temp_summary_panels/{all_samples}_summary.csv",
        mask = "results/temp_masks/{all_samples}",
        mask_files = "results/temp_masks/{all_samples}/{all_samples}.tiff"
    output:
        output_file = "results/intensities/{all_samples}.csv",
        tmp_dir= temp(directory("results/temp_intensities/{all_samples}")),
        tmp_file=temp("results/temp_intensities/{all_samples}/{all_samples}.csv")
    log:
        stdout="logs/steinbock_create_intensities/{all_samples}.stdout",
        stderr="logs/steinbock_create_intensities/{all_samples}.stderr"
    container:
        steinbock_container
    shell: 
        "mkdir -p {output.tmp_dir} > {log.stdout} 2> {log.stderr} "
        " && steinbock measure intensities --aggr mean --img {input.img} --panel {input.panel} --masks {input.mask} -o {output.tmp_dir} >> {log.stdout} 2>> {log.stderr} "
        " && cp {output.tmp_dir}/*.csv {output.output_file} >> {log.stdout} 2>> {log.stderr}"


rule steinbock_create_regionprops:
    input:
        img = "results/temp_img/{all_samples}",
        img_files = "results/temp_img/{all_samples}/{all_samples}.tiff",
        mask = "results/temp_masks/{all_samples}",
        mask_files = "results/temp_masks/{all_samples}/{all_samples}.tiff"
    output:
        output_file="results/regionprops/{all_samples}.csv",
        tmp_dir= temp(directory("results/temp_regionprops/{all_samples}")),
        tmp_file=temp("results/temp_regionprops/{all_samples}/{all_samples}.csv")
    log:
        stdout="logs/steinbock_create_regionprops/{all_samples}.stdout",
        stderr="logs/steinbock_create_regionprops/{all_samples}.stderr"
    container:
        steinbock_container
    shell: 
        "mkdir -p {output.tmp_dir}  > {log.stdout} 2> {log.stderr} "
        " && steinbock measure regionprops "
        " --img {input.img} "
        " --masks {input.mask} "
        " -o {output.tmp_dir} "
        " area centroid major_axis_length minor_axis_length "
        " eccentricity euler_number orientation perimeter solidity  >> {log.stdout} 2>> {log.stderr} "
        " && cp {output.tmp_file} {output.output_file}  >> {log.stdout} 2>> {log.stderr} "


rule steinbock_create_neighbors:
    params:
        #output_dir = "results/temp_neighbors"
        output_dir = lambda w, output: os.path.join(*os.path.normpath(output[2]).split(os.path.sep)[:2])
    input:
        img = "results/temp_img/{all_samples}",
        img_files = "results/temp_img/{all_samples}/{all_samples}.tiff",
        mask = "results/temp_masks/{all_samples}",
        mask_files = "results/temp_masks/{all_samples}/{all_samples}.tiff"
    output:
        output_file="results/neighbors/{all_samples}.csv",
        tmp_dir= temp(directory("results/temp_neighbors/{all_samples}")),
        tmp_file=temp("results/temp_neighbors/{all_samples}/{all_samples}.csv")
    log:
        stdout="logs/steinbock_create_neighbors/{all_samples}.stdout",
        stderr="logs/steinbock_create_neighbors/{all_samples}.stderr"
    container:
        steinbock_container
    shell: 
        "steinbock measure neighbors "
        " --masks {input.mask} "
        " -o {output.tmp_dir} "
        " --type centroids --dmax 15  > {log.stdout} 2> {log.stderr} "
        " && cp {output.tmp_file} {output.output_file}  >> {log.stdout} 2>> {log.stderr} "


rule create_spe:
    params:
        #steinbock_dir = "results"
        steinbock_dir = lambda w, input: os.path.normpath(input[0]).split(os.path.sep)[0]
    input:
        intensities = "results/intensities/{all_samples}.csv",
        regionpropts = "results/regionprops/{all_samples}.csv",
        neighbors = "results/neighbors/{all_samples}.csv",
        panel_file = "results/temp_summary_panels/{all_samples}_summary.csv",
        channel_metadata_file = channel_metadata_file
    output:
        "results/spe/raw/SPE_raw_{all_samples}.rds"
    log:
        stdout="logs/create_spe/{all_samples}.stdout",
        stderr="logs/create_spe/{all_samples}.stderr"
    container:
        "docker://retogerber/imcr:latest"
    script:
        "scripts/prepare_spe.R"


rule combine_spe:
    input:
        spes = get_spe_from_images_csv
    output:
        spe="results/spe/raw/SPE_combined.rds"
    log:
        stdout="logs/combine_spe/log.stdout",
        stderr="logs/combine_spe/log.stderr"
    container:
        "docker://retogerber/imcr:latest"
    script:
        "scripts/combine_spe.R"

rule filter_spe:
    params:
        min_cell_area=min_cell_area
    input:
        spe="results/spe/raw/SPE_combined.rds",
        masks=get_mask_from_images_csv
    output:
        spe="results/spe/raw/SPE_combined_markedfilt.rds"
    log:
        stdout="logs/filter_spe/log.stdout",
        stderr="logs/filter_spe/log.stderr"
    container:
        "docker://retogerber/imcr:latest"
    script:
        "scripts/filter_spe.R"

rule visualize_filter_spe:
    params:
        min_cell_area=min_cell_area
    input:
        spe="results/spe/raw/SPE_combined_markedfilt.rds",
        masks=get_mask_from_images_csv,
        images=get_img_from_images_csv
    output:
        html="results/html/visualize_filter_spe.html"
    log:
        stdout="logs/visualize_filter_spe/log.stdout",
        stderr="logs/visualize_filter_spe/log.stderr"
    container:
        "docker://retogerber/imcr:latest"
    script:
        "scripts/visualize_filter_spe.Rmd"

rule batch_correction_spe:
    params:
        cols_for_batch=cols_for_batch
    threads: 32
    input:
        spe="results/spe/raw/SPE_combined_markedfilt.rds",
        sample_metadata=sample_metadata
    output:
        spe="results/spe/filt/SPE_combined_01.rds"
    log:
        stdout="logs/batch_correction_spe/log.stdout",
        stderr="logs/batch_correction_spe/log.stderr"
    container:
        "docker://retogerber/imcr:latest"
    script:
        "scripts/batch_correction_spe.R"

rule visualize_batch_spe:
    params:
        cols_for_batch=cols_for_batch
    threads: 16
    input:
        spe="results/spe/filt/SPE_combined_01.rds"
    output:
        html="results/html/visualize_batch_spe.html"
    log:
        stdout="logs/visualize_batch_spe/log.stdout",
        stderr="logs/visualize_batch_spe/log.stderr"
    container:
        "docker://retogerber/imcr:latest"
    script:
        "scripts/visualize_batch_spe.Rmd"

rule batch_evaluation_spe:
    threads: 32
    params:
        k=k_cms,
        cols_for_batch=cols_for_batch
    input:
        spe="results/spe/filt/SPE_combined_01.rds"
    output:
        spe="results/spe/filt/SPE_combined_01_cms.rds"
    log:
        stdout="logs/batch_evaluation_spe/log.stdout",
        stderr="logs/batch_evaluation_spe/log.stderr"
    container:
        "docker://retogerber/imcr:latest"
    script:
        "scripts/batch_evaluation_spe.R"

rule visualize_batch_evaluation_spe:
    threads: 1
    params:
        cols_for_batch=cols_for_batch
    input:
        spe="results/spe/filt/SPE_combined_01_cms.rds"
    output:
        html="results/html/visualize_batch_evaluation_spe.html"
    log:
        stdout="logs/visualize_batch_evaluation_spe/log.stdout",
        stderr="logs/visualize_batch_evaluation_spe/log.stderr"
    container:
        "docker://retogerber/imcr:latest"
    script:
        "scripts/visualize_batch_evaluation_spe.Rmd"



rule cluster_spe:
    threads: 16
    input:
        spe="results/spe/filt/SPE_combined_01.rds"
    output:
        spe="results/spe/filt/SPE_combined_02.rds"
    log:
        stdout="logs/cluster_spe/log.stdout",
        stderr="logs/cluster_spe/log.stderr"
    container:
        "docker://retogerber/imcr:latest"
    script:
        "scripts/cluster_spe.R"


rule visualize_cluster_spe:
    threads: 16
    input:
        spe="results/spe/filt/SPE_combined_02.rds"
    output:
        html="results/html/visualize_cluster_spe.html"
    log:
        stdout="logs/visualize_cluster_spe/log.stdout",
        stderr="logs/visualize_cluster_spe/log.stderr"
    container:
        "docker://retogerber/imcr:latest"
    script:
        "scripts/visualize_cluster_spe.Rmd"


rule annotate_spe:
    threads: 1
    params:
        clustering_used=clustering_used
    input:
        spe="results/spe/filt/SPE_combined_02.rds",
        clustering_annotation_file=clustering_annotation_file
    output:
        spe="results/spe/filt/SPE_combined_03.rds"
    log:
        stdout="logs/annotate_spe/log.stdout",
        stderr="logs/annotate_spe/log.stderr"
    container:
        "docker://retogerber/imcr:latest"
    script:
        "scripts/annotate_spe.R"



rule gather_reports:
    output:
        report = report(directory("results/html"), patterns=["{name}.html"], caption="report/report.rst"),
        empty_file = temp("results/html/.empty")
    log:
        stdout="logs/gather_reports/log.stdout",
        stderr="logs/gather_reports/log.stderr"
    container:
        steinbock_container
    shell:
        "touch {output.empty_file} > {log.stdout} 2> {log.stderr} "
